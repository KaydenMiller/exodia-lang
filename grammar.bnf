// -----------------------------------------
// Lexical grammar

%lex

%%

\/\/.*              /* skip comments */
\/\*(.|\s)*\*\/     /* skip block comments */        

\s+                 /* skip whitespace */


// -----------------------------------------
// Keywords

\bfn\b              return 'FN'
\breturn\b          return 'RETURN'
\bif\b              return 'IF'
\belse\b            return 'ELSE'
\btrue\b            return 'TRUE'
\bfalse\b           return 'FALSE'
\bnull\b            return 'NULL'

\d+                 return 'NUMBER'
\w+                 return 'IDENTIFIER'

[\=!]\=             return 'EQUALITY_OPERATOR'
[><]\=?             return 'RELATIONAL_OPERATOR'

[+\-]               return 'ADDITIVE_OPERATOR'
[*/]                return 'MULTIPLICATIVE_OPERATOR'

/lex

// -----------------------------------------
// Module Include

%{

function BinaryExpression(left, op, right) {
  return {
    type: 'BinaryExpression',
    operator: op,
    left,
    right,
  };
}

function Literal(prefix, value) {
  return {
    type: prefix + 'Literal',
    value
  }
}

function IfStatement(test, consequent, alternate) {
  return {
    type: 'IfStatement',
    test,
    consequent,
    alternate
  }
}

%}

/**
 * The `THEN` precedence is lower than that of `ELSE`, so no shift-reduce conflict, and no dangling-else
 */
%nonassoc THEN
%nonassoc ELSE

// -----------------------------------------
// Syntactic grammar

%%

Program
  : StatementList
    {
      $$ = {
        type: 'Program',
        body: $1,
      };
    }
  ;

StatementList
  : Statement
    {
      $$ = [$1];
    }
  | StatementList Statement
    {
      $1.push($2);
      $$ = $1;
    }
  ;

OptStatementList
  : StatementList
  | /* empty */
    { $$ = []; }
  ;

Statement
  : ExpressionStatement
  | EmptyStatement
  | BlockStatement
  | FunctionDeclaration
  | ReturnStatement
  | IfStatement
  ;

IfStatement
  : IF '(' Expression ')' Statement %prec THEN
    { $$ = IfStatement($3, $5); }
  | IF '(' Expression ')' Statement ELSE Statement
    { $$ = IfStatement($3, $5, $7); }
  ;

ReturnStatement
  : RETURN OptExpression ';' 
    {
      $$ = {
        type: 'ReturnStatement',
        argument: $2,
      };
    }
  ;

OptExpression
  : Expression
  | /* Empty */
    { $$ = null; }
  ;

FunctionDeclaration
  : FN Identifier '(' OptFormalParameterList ')' BlockStatement
    {
      $$ = {
        type: 'FunctionDeclaration',
        name: $Identifier,
        params: $4,
        body: $6
      };
    }
  ;

Identifier
  : IDENTIFIER
    {
      $$ = {
        type: 'Identifier',
        value: $1,
      };
    }
  ;

OptFormalParameterList
  : FormalParameterList
  | /* Empty */
    {
      $$ = [];
    }
  ;

FormalParameterList
  : Identifier 
    {
      $$ = [$1];
    }
  | FormalParameterList ',' Identifier 
    {
      $1.push($3);
      return $1;
    }
  ;

ExpressionStatement
  : Expression ';'
    {
      $$ = {
        type: 'ExpressionStatement',
        expression: $1,
      };
    }
  ;

EmptyStatement
  : ';' 
    {
      $$ = {
        type: 'EmptyStatement',
      };
    }
  ;

BlockStatement
  : '{' OptStatementList '}' 
    {
      $$ = {
        type: 'BlockStatement',
        body: $2,
      };
    }
  ;

Expression
  : EqualityExpression
  ;

EqualityExpression
  : RelationalExpression
  | EqualityExpression EQUALITY_OPERATOR RelationalExpression
    {
      $$ = BinaryExpression($1, $2, $3) 
    }
  ;

RelationalExpression
  : AdditiveExpression
  | RelationalExpression RELATIONAL_OPERATOR AdditiveExpression
    {
      $$ = BinaryExpression($1, $2, $3) 
    }
  ;

AdditiveExpression
  : AdditiveExpression ADDITIVE_OPERATOR MultiplicativeExpression
    {
      $$ = BinaryExpression($1, $2, $3) 
    }
  | MultiplicativeExpression
  ;

MultiplicativeExpression
  : MultiplicativeExpression MULTIPLICATIVE_OPERATOR PrimaryExpression 
    {
      $$ = BinaryExpression($1, $2, $3) 
    }
  | PrimaryExpression
  ;

PrimaryExpression
  : Literal 
  | Identifier
  | ParenthesizedExpression
  ;

Literal
  : NumericLiteral 
  | TrueLiteral
  | FalseLiteral
  | NullLiteral
  ;

TrueLiteral
  : TRUE { $$ = Literal('Boolean', true); }
  ;

FalseLiteral
  : FALSE { $$ = Literal('Boolean', false); }
  ;

NullLiteral
  : NULL { $$ = Literal('Null', null); }
  ;

NumericLiteral
  : NUMBER { $$ = Literal('Numeric', Number($1)); }
  ;

ParenthesizedExpression
  : '(' Expression ')' { $$ = $2; }
  ;